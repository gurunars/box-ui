apply from: '../all-libs.gradle'

apply plugin: 'org.jetbrains.dokka-android'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

publishing {
    publications {
        BintrayPublication(MavenPublication) {
            groupId moduleGroup
            artifactId moduleName
            version rootProject.version
            artifact("$buildDir/outputs/aar/$moduleName-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                def items =
                    configurations.api.allDependencies +
                    configurations.implementation.allDependencies +
                    configurations.compile.allDependencies
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                items.each {
                    if(it.group != null && (it.name != null || "unspecified" == it.name) && it.version != null)
                    {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

artifacts {
    archives sourcesJar
}

bintray {
    user = bintrayUser
    key = bintrayApikey

    configurations = ['archives']
    pkg {
        repo = "maven"
        name = moduleName
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = [license]
        publish = true
        publications = ['BintrayPublication']
    }
}

bintrayUpload.dependsOn([build, sourcesJar])
