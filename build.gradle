buildscript {
    ext.kotlin_version = '1.1.2-3'
    ext.anko_version = '0.10.0-rc'
    ext.dokka_version = '0.9.14'

    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.3'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.2'
        classpath "com.github.dcendents:android-maven-gradle-plugin:1.4.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-android-gradle-plugin:$dokka_version"
    }
}

allprojects {
    repositories {
        jcenter()
        mavenLocal()
        maven {url "https://clojars.org/repo/"}
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

task dokka() {
    description 'Aggregate API docs of all subprojects with custom styles.'
    group JavaBasePlugin.DOCUMENTATION_GROUP
    dependsOn project.subprojects.findAll {
        module -> module.plugins.findPlugin("com.android.library")
    }.collect { it.getTasksByName("dokka", true) }
    doLast {
        def styleIn = new File("style.css")
        def styleOut = new File("html-docs/style.css")
        styleOut.write(styleIn.readLines().join("\n"))

        def indexFile = new File("html-docs/index.html")

        def projects = project.subprojects.findAll {
            module -> module.plugins.findPlugin("com.android.library")
        }.collect {
            def description = ""
            if (it.description != null) {
                description = "<p>${it.description}</p>"
            }
            """
            <div class="section">
                <h3><a href="${it.name}">${it.name} (${it.version})</a></h3>
                ${description}
                <code>
                compile ('${it.group}.${it.name}:${it.version}@aar') {
                    transitive = true
                }
                </code>
            </div>
            """
        }

        indexFile.write("""
            <html>
                <head>
                    <meta charset="UTF-8">
                    <title>${project.name} ($project.version)</title>
                    <link rel="stylesheet" href="style.css">
                </head>
                <body>
                    ${projects.join("\n")}
                </body>
            </html>
        """)
    }
}

apply plugin: StyledDokka
